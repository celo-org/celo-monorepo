apply plugin: "com.android.application"

import com.android.build.OutputFile

/**
 * The react.gradle file registers a task for each build variant (e.g. bundleDebugJsAndAssets
 * and bundleReleaseJsAndAssets).
 * These basically call `react-native bundle` with the correct arguments during the Android build
 * cycle. By default, bundleDebugJsAndAssets is skipped, as in debug/dev mode we prefer to load the
 * bundle directly from the development server. Below you can see all the possible configurations
 * and their defaults. If you decide to add a configuration block, make sure to add it before the
 * `apply from: "../../node_modules/react-native/react.gradle"` line.
 *
 * project.ext.react = [
 *   // the name of the generated asset file containing your JS bundle
 *   bundleAssetName: "index.android.bundle",
 *
 *   // the entry file for bundle generation
 *   entryFile: "index.android.js",
 *
 *   // whether to bundle JS and assets in debug mode
 *   bundleInDebug: false,
 *
 *   // whether to bundle JS and assets in release mode
 *   bundleInRelease: true,
 *
 *   // whether to bundle JS and assets in another build variant (if configured).
 *   // See http://tools.android.com/tech-docs/new-build-system/user-guide#TOC-Build-Variants
 *   // The configuration property can be in the following formats
 *   //         'bundleIn${productFlavor}${buildType}'
 *   //         'bundleIn${buildType}'
 *   // bundleInFreeDebug: true,
 *   // bundleInPaidRelease: true,
 *   // bundleInBeta: true,
 *
 *   // whether to disable dev mode in custom build variants (by default only disabled in release)
 *   // for example: to disable dev mode in the staging build type (if configured)
 *   devDisabledInStaging: true,
 *   // The configuration property can be in the following formats
 *   //         'devDisabledIn${productFlavor}${buildType}'
 *   //         'devDisabledIn${buildType}'
 *
 *   // the root of your project, i.e. where "package.json" lives
 *   root: "../../",
 *
 *   // where to put the JS bundle asset in debug mode
 *   jsBundleDirDebug: "$buildDir/intermediates/assets/debug",
 *
 *   // where to put the JS bundle asset in release mode
 *   jsBundleDirRelease: "$buildDir/intermediates/assets/release",
 *
 *   // where to put drawable resources / React Native assets, e.g. the ones you use via
 *   // require('./image.png')), in debug mode
 *   resourcesDirDebug: "$buildDir/intermediates/res/merged/debug",
 *
 *   // where to put drawable resources / React Native assets, e.g. the ones you use via
 *   // require('./image.png')), in release mode
 *   resourcesDirRelease: "$buildDir/intermediates/res/merged/release",
 *
 *   // by default the gradle tasks are skipped if none of the JS files or assets change; this means
 *   // that we don't look at files in android/ or ios/ to determine whether the tasks are up to
 *   // date; if you have any other folders that you want to ignore for performance reasons (gradle
 *   // indexes the entire tree), add them here. Alternatively, if you have JS files in android/
 *   // for example, you might want to remove it from here.
 *   inputExcludes: ["android/**", "ios/**"],
 *
 *   // override which node gets called and with what additional arguments
 *   nodeExecutableAndArgs: ["node"],
 *
 *   // supply additional arguments to the packager
 *   extraPackagerArgs: []
 * ]
 */

project.ext.react = [
        entryFile        : "index.js",
        extraPackagerArgs: ["--max-workers=2"],
        bundleInIntegration: true,
        devDisabledInIntegration: true,
        bundleInStaging: true,
        devDisabledInStaging: true,
        bundleInAlfajores: true,
        devDisabledInAlfajores: true,
        bundleInPilot: true,
        devDisabledInPilot: true,
]

project.ext.envConfigFiles = [
    debug: ".env",
    integration: ".env.integration",
    staging: ".env.staging",
    release: ".env.production",
    alfajores: ".env.alfajores",
    pilot: ".env.pilot",
]

apply from: project(':react-native-config').projectDir.getPath() + "/dotenv.gradle"
apply from: "../../node_modules/react-native/react.gradle"
apply from: "../../../../node_modules/react-native-sentry/sentry.gradle"


 /**
 * Build for Detox has to have a universal apk and should be minSdkVersion >= 18
 * Use the taskname to recognize the build is for detox
 */
def isDetoxTestBuild = !gradle.startParameter.taskNames.contains("assembleAndroidTest")

/**
 * Run Proguard to shrink the Java bytecode in release builds.
 */
def enableProguardInReleaseBuilds = true

def appVersionCode = Integer.valueOf(System.env.CIRCLE_BUILD_NUM ?: VERSION_CODE)

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "org.celo.mobile"
        minSdkVersion isDetoxTestBuild ? rootProject.ext.minSdkVersion : 18
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode appVersionCode
        versionName "1.4.2"
        testBuildType System.getProperty('testBuildType', 'debug')
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resValue "string", "build_config_package", "org.celo.mobile"
        missingDimensionStrategy 'react-native-camera', 'general'
    }
    signingConfigs {
        release {
            if (project.hasProperty('CELO_RELEASE_STORE_FILE')) {
                storeFile file(CELO_RELEASE_STORE_FILE)
                storePassword System.getenv("CELO_RELEASE_STORE_PASSWORD")
                keyAlias CELO_RELEASE_KEY_ALIAS
                keyPassword System.getenv("CELO_RELEASE_KEY_PASSWORD")
            }
        }
    }
    splits {
        abi {
            reset()
            /**
             * Set this to true to create two separate APKs instead of one:
             *   - An APK that only works on ARM devices
             *   - An APK that only works on x86 devices
             * The advantage is the size of the APK is reduced by about 4MB.
             * Upload all the APKs to the Play Store and people will download
             * the correct one based on the CPU architecture of their device.
             */
            enable isDetoxTestBuild
            universalApk false  // If true, also generate a universal APK
            include "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            multiDexEnabled true
        }

        integration {
            applicationIdSuffix ".integration"
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
            matchingFallbacks = ['release']
        }

        staging {
            applicationIdSuffix ".staging"
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
            matchingFallbacks = ['release']
        }

        release {
            applicationIdSuffix ".production"
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }

        alfajores {
            applicationIdSuffix ".alfajores"
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
            matchingFallbacks = ['release']
        }

        pilot {
            applicationIdSuffix ".pilot"
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
            matchingFallbacks = ['release']
        }
    }
    // applicationVariants are e.g. debug, release
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            // For each separate APK per architecture, set a unique version code as described here:
            // http://tools.android.com/tech-docs/new-build-system/user-guide/apk-splits
            def versionCodes = ["armeabi-v7a":1, "x86":2, "arm64-v8a": 3, "x86_64": 4]
            def abi = output.getFilter(OutputFile.ABI)
            if (abi != null) {  // null for the universal-debug, universal-release variants
                output.versionCodeOverride =
                        versionCodes.get(abi) * 1048576 + defaultConfig.versionCode
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/-no-jdk.kotlin_module'
        exclude 'META-INF/androidx.exifinterface_exifinterface.version'
    }
}

dependencies {
    implementation project(':react-native-install-referrer')
    implementation project(':react-native-send-intent')
    // implementation project(':react-native-screens')
    implementation project(':react-native-webview')
    implementation project(':@react-native-community_netinfo')
    implementation project(':react-native-gesture-handler')
    implementation project(':@segment_analytics-react-native-firebase')
    implementation project(':@segment_analytics-react-native')
    implementation project(':react-native-camera')
    implementation project(':react-native-mail')
    implementation project(':react-native-splash-screen')
    implementation project(':react-native-version-check')
    implementation project(':react-native-android-open-settings')
    implementation project(':react-native-udp')
    implementation project(':react-native-localize')
    implementation project(':react-native-config')
    implementation project(':react-native-tcp')
    implementation project(':react-native-sentry')
    implementation project(':react-native-secure-randombytes')
    implementation project(':react-native-svg')
    implementation project(':react-native-contacts')
    implementation project(':react-native-keep-awake')
    implementation project(':react-native-device-info')
    implementation project(':react-native-fs')
    implementation project(':react-native-geth')
    implementation project(':react-native-flag-secure-android')
    implementation project(':react-native-firebase')
    implementation project(':react-native-confirm-device-credentials')
    implementation project(':react-native-restart-android')
    implementation project(':react-native-sms-retriever')
    implementation project(':react-native-share')
    implementation project(':react-native-secure-key-store')

    androidTestImplementation('com.wix:detox:+') { transitive = true }
    androidTestImplementation 'junit:junit:4.12'

    implementation 'com.google.android.gms:play-services-base:16.1.0'
    implementation 'com.google.firebase:firebase-core:16.0.8'
    implementation 'com.google.firebase:firebase-messaging:17.6.0'
    implementation 'com.google.firebase:firebase-database:16.1.0'
    implementation 'com.google.firebase:firebase-auth:16.2.1'
    implementation 'com.google.firebase:firebase-invites:16.1.1'
    implementation 'com.google.firebase:firebase-storage:16.1.0'
    implementation 'ru.ivanarh.ndcrash:jndcrash-libunwind:0.7'

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "com.android.support:appcompat-v7:${rootProject.ext.supportLibVersion}"
    implementation 'com.facebook.react:react-native:+'  // From node_modules
    implementation 'com.android.support:multidex:1.0.3'
}

// Run this once to be able to run the application with BUCK
// puts all compile dependencies into folder libs for BUCK to use
task copyDownloadableDepsToLibs(type: Copy) {
    from configurations.compile
    into 'libs'
}

def enableFirebase = project.env.get("FIREBASE_ENABLED")

if (enableFirebase.toBoolean()){
  println 'Firebase is enabled in .env variable'
  apply plugin: 'com.google.gms.google-services'
} else {
  println 'Info: Firebase is disabled in .env variable'
}

